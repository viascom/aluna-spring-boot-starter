plugins {
    id "idea"
    id "java-library"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "1.6.20"
    id "org.jetbrains.kotlin.kapt" version "1.6.20"
    id "org.springframework.boot" version "2.6.6"
    id "org.liquibase.gradle" version "2.1.1"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

group "io.viascom.discord.bot"
version "5.0.0-alpha.9"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

tasks.bootJar {enabled = false}
tasks.jar {enabled = true}

jar {
    baseName "aluna-spring-boot-starter"
    archiveClassifier.set("")
    manifest {
        attributes "Implementation-Version": "${project.version}"
    }
//    into("META-INF/maven/$project.group/discord-bot-spring-boot-starter") {
//        from { generatePomFileForMavenPublication }
//        rename ".*", "pom.xml"
//    }
}

springBoot {
    buildInfo()
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        name "m2-dv8tion"
        url "https://m2.dv8tion.net/releases"
    }
}

dependencies {

    //====== Kotlin ======
    api "org.jetbrains.kotlin:kotlin-stdlib"
    api "org.jetbrains.kotlin:kotlin-reflect"

    //====== Spring ======
    api ("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
        exclude module: "spring-boot-starter-json"
    }
    api "org.springframework.boot:spring-boot-starter-undertow"
    api "org.springframework.boot:spring-boot-starter-actuator"
    kapt "org.springframework.boot:spring-boot-configuration-processor"

    //====== Database ======
    implementation "org.postgresql:postgresql:42.3.3"
    implementation "com.vladmihalcea:hibernate-types-52:2.14.1"
    implementation "org.liquibase:liquibase-core"
    liquibaseRuntime "org.liquibase:liquibase-core"
    liquibaseRuntime "org.postgresql:postgresql"

    //====== JDA ======
    //implementation "net.dv8tion:JDA:4.4.0_352"
    api "net.dv8tion:JDA:5.0.0-alpha.9"

    //====== Retrofit2 ======
    api "com.squareup.retrofit2:retrofit:2.9.0"
    api "com.squareup.retrofit2:converter-gson:2.9.0"

    //====== OpenAPI ======
    api "org.springdoc:springdoc-openapi-kotlin:1.5.12"
    api "org.springdoc:springdoc-openapi-ui:1.6.6"

    //====== Gson ======
    api "com.google.code.gson:gson:2.9.0"
    api "com.fatboyindustrial.gson-javatime-serialisers:gson-javatime-serialisers:1.1.1"

    //====== Logging ======
    implementation "org.zalando:logbook-logstash:2.14.0"
    implementation "org.zalando:logbook-spring-boot-starter:2.14.0"
    api "com.datadoghq:dd-trace-api:0.98.1"

    implementation "de.codecentric:spring-boot-admin-starter-client:2.6.4"
    api "org.apache.commons:commons-lang3:3.12.0"
    api "com.google.guava:guava:31.1-jre"
    api 'com.jayway.jsonpath:json-path:2.7.0'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
}

classes {
    doLast {
        // Copy generated spring-configuration-metadata.json into jar resources
        file("build/tmp/kapt3/classes/main/META-INF/spring-configuration-metadata.json").renameTo(file("build/resources/main/META-INF/spring-configuration-metadata.json"))
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                groupId = 'io.viascom.discord.bot'
                name = 'aluna-spring-boot-starter'
                description = 'Viascom core library for Discord bots.'
                url = 'https://github.com/viascom/aluna-spring-boot-starter'
                packaging = 'jar'

                scm {
                    url = 'https://github.com/viascom/aluna-spring-boot-starter'
                    connection = 'scm:git://github.com/viascom/aluna-spring-boot-starter.git'
                    developerConnection = 'scm:git://github.com/viascom/aluna-spring-boot-starter.git'
                }

                developers {
                    developer {
                        id = 'itsmefox'
                        name = 'Patrick BÃ¶sch'
                        email = 'patrick.boesch@viascom.email'
                        organizationUrl = 'https://viascom.io/'
                    }
                    developer {
                        id = 'botscripter'
                        name = 'Nikola Stankovic'
                        email = 'nikola.stankovic@viascom.email'
                        organizationUrl = 'https://viascom.io/'
                    }
                }
            }
        }
    }
}
